# Maintainer: taotieren <admin@taotieren.com>
# Contributor: neko0xff <neko_0xff@protonmail.com>

pkgname=at32-ide-bin
pkgver=1.0.08
pkgrel=4
pkgdesc="Artery AT32 IDE 是跨平台 ARM 嵌入式系统的整合開發環境。它包含一系列的 Eclipse 插件和工具。該插件可使用戶在 AT32 IDE 下建置和除錯 AT32 MCU。"
arch=('x86_64')
url="https://www.arterytek.com/cn/product/AT32F423.jsp"
license=('Commercial')
provides=(at32-ide AT32IDE)
conflicts=()
replaces=()
depends=('java-runtime' 'desktop-file-utils' 'gcc' 'hidapi' 'ncurses' 'ncurses5-compat-libs' 'arm-none-eabi-gdb' 'arm-none-eabi-gcc' 'openocd')
makedepends=('libarchive' 'sed')
optdepends=('artery-isp-console-bin: Artery ISP Console 是一款基於 MCU Bootloader 的命令列的應用程式。用戶使用時,可以通過 UART 或者 USB 端口配置操作 Artery 的 MCU。'
            'jlink-software-and-documentation: Segger JLink software & documentation pack for Linux')
backup=()
options=()
install=${pkgname}.install
_pkg_file_name=AT32_IDE_V${pkgver}_Linux-${arch}
source=("${_pkg_file_name}.zip::https://www.arterytek.com/download/TOOL/AT32_IDE_V${pkgver}_Linux-x86_64.zip")
sha256sums=('52c080897dd7377513e973da792e6cbc549026026a5684b07b54da8cb474ed19')
noextract=()

package() {
    install -dm0755 "${pkgdir}/opt/artery32/${pkgname%-bin}/"

    bsdtar -xf  ${srcdir}/AT32IDE_V${pkgver}_Linux-X86_64.tar.gz -C "${pkgdir}/opt/artery32/${pkgname%-bin}/"

    install -Dm0755 /dev/stdin "${pkgdir}/usr/bin/${pkgname%-bin}" << EOF
#!/bin/bash
/opt/artery32/${pkgname%-bin}/AT32IDE "\$@"
EOF

    chmod 644 "${pkgdir}/opt/artery32/${pkgname%-bin}/AT32IDE.ini"

    install -Dm0644 "${pkgdir}/opt/artery32/${pkgname%-bin}/icon.xpm" "${pkgdir}/usr/share/pixmaps/${pkgname%-bin}.xpm"

    install -Dm0644 "${pkgdir}/opt/artery32/${pkgname%-bin}/OpenOCD/contrib/60-openocd.rules" "${pkgdir}/etc/udev/rules.d/60-openocd-${pkgname%-bin}.rules"
    sed -i 's|openocd_rules|openocd_at32_ide_rules|g' "${pkgdir}/etc/udev/rules.d/60-openocd-${pkgname%-bin}.rules"

    install -Dm0644 /dev/stdin "${pkgdir}/usr/share/metainfo/com.arterytek.at32ide.metainfo.xml" << EOF
<?xml version="1.0" encoding="UTF-8"?>
<component type="desktop-application">
  <id>com.arterytek.at32ide</id>

  <name>AT32IDE</name>
  <summary>AT32IDE</summary>

  <metadata_license>MIT</metadata_license>
  <project_license>GPL-2.0-or-later</project_license>

  <description>
    <p>
      Artery AT32 IDE 是跨平台 ARM 嵌入式系统的整合開發環境。它包含一系列的 Eclipse 插件和工具。該插件可使用者在 AT32 IDE 下建置和除錯 AT32 MCU。
    </p>
  </description>

  <launchable type="desktop-id">com.arterytek.at32ide.desktop</launchable>
</component>
EOF

    install -Dm0644 /dev/stdin "${pkgdir}/usr/share/applications/com.arterytek.at32ide.desktop" << EOF
[Desktop Entry]
Version=1.0
Type=Application

Name=AT32IDE
Comment=AT32IDE
Categories=Development;Electronics;

Icon=${pkgname%-bin}
Exec=${pkgname%-bin}
Terminal=false
EOF
}
